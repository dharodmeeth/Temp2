ALTER PROCEDURE [dbo].[DBA_ETLSQLagentJobCheck_report] 
AS
BEGIN
    SET NOCOUNT ON;

    DECLARE 
        @htmltable NVARCHAR(MAX),
        @MailProfile VARCHAR(50), 
        @SubjectLine NVARCHAR(255);

    -- Get mail profile
    SELECT TOP 1 @MailProfile = [name] FROM msdb..sysmail_profile;
    SELECT @SubjectLine = 'ETL Job Report For V1WBSINVSQ07L1';

    -- Build the HTML table header
    SET @htmltable = 
    N'
    <!DOCTYPE html>
    <html lang="en">
    <head>
        <meta charset="UTF-8">
        <title>SQL Agent Job Check</title>
        <style>
            body { font-family: Arial, sans-serif; font-size: 10pt; background-color: #F5F5F5; margin: 20px; }
            table { width: 100%; border: 1px solid black; border-collapse: collapse; background-color: #DCDFDF; text-align: center; }
            th { border: 1px solid white; background-color: #7FB1B3; padding: 10px; color: white; font-weight: bold; }
            td { border: 1px solid black; padding: 8px; }
        </style>
    </head>
    <body>
    <h2>ETL Job Report For V1WBSINVSQ07L1</h2>
    <table>
        <tr>
            <th>Job Name</th>
            <th>Message</th>
            <th>Date</th>
            <th>Status</th>
        </tr>';

    ;WITH JobRuns AS (
        SELECT 
            SJ.name,
            SJH.message,
            RunDateTime = CONVERT(DATETIME, RTRIM(SJH.run_date)) + 
                          (SJH.run_time / 10000) * (1.0/24) + 
                          ((SJH.run_time % 10000) / 100) * (1.0/1440) + 
                          (SJH.run_time % 100) * (1.0/86400),
            SJH.run_status
        FROM msdb..sysjobhistory SJH
        JOIN msdb..sysjobs SJ 
            ON SJH.job_id = SJ.job_id  
        WHERE SJH.step_id = 0
          AND SJ.name LIKE 'DXRA%'
    )
    , LastRun AS (
        SELECT TOP 1 *
        FROM JobRuns
        ORDER BY RunDateTime DESC
    )
    SELECT @htmltable = @htmltable + 
    STRING_AGG(
        '<tr>
            <td>' + CAST(name as varchar(max)) + '</td>
            <td>' + message + '</td>
            <td>' + CONVERT(VARCHAR, RunDateTime, 100) + '</td>
            <td>' + 
                CASE 
                    WHEN run_status = 0 THEN 'Failed'
                    WHEN run_status = 1 THEN 'Succeeded'
                    WHEN run_status = 2 THEN 'Retry'
                    WHEN run_status = 3 THEN 'Cancelled'
                    ELSE 'Unknown'
                END
            + '</td>
        </tr>', 
        ''
    )
    FROM LastRun;

    -- Close the HTML table
    SET @htmltable = @htmltable + 
    N'
    </table>
    </body>
    </html>';

    -- Send email
    EXEC msdb.dbo.sp_send_dbmail 
        @profile_name = @MailProfile,
        @recipients = 'luiza.vladescu@Fisglobal.com;meeth.dharod@Fisglobal.com;shivam.gupta3@fisglobal.com',
        @subject = @SubjectLine,
        @body = @htmltable,
        @body_format = 'HTML';
END
GO
