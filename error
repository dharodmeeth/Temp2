DECLARE @DB sysname, @sql NVARCHAR(MAX);

DECLARE curDB CURSOR FOR
SELECT [name]
FROM sys.databases
WHERE [name] NOT IN ('master','model','msdb','tempdb','DefaultDB','admindba','iws_admin');

OPEN curDB
FETCH NEXT FROM curDB INTO @DB
WHILE @@FETCH_STATUS = 0
BEGIN
    SET @sql = '
    USE '+QUOTENAME(@DB)+';

    -- open certificate first if needed
    -- OPEN MASTER KEY DECRYPTION BY CERTIFICATE CertificateETLFTP;

    INSERT INTO admindba.dbo.ETLTempTable (DatabaseName, ClientCode, FtpUserName, ftppass)
    SELECT 
        DB_NAME() AS DatabaseName,
        ConfigurationValue AS ClientCode,
        (SELECT ConfigurationValue FROM dbo.ETL_Configuration WHERE ConfigurationName = ''ftpUserName'') AS FtpUserName,
        CONVERT(NVARCHAR(MAX), DECRYPTBYKEYAUTOCERT(CERT_ID(''CertificateETLFTP''), NULL, ConfigurationEncrypted)) AS ftppass
    FROM dbo.ETL_Configuration
    WHERE ConfigurationName IN (''SourceCode'',''ClientID'')
      AND DECRYPTBYKEYAUTOCERT(CERT_ID(''CertificateETLFTP''), NULL, ConfigurationEncrypted) IS NOT NULL;';

    PRINT @sql; -- see the generated SQL
    EXEC(@sql);

    FETCH NEXT FROM curDB INTO @DB
END

CLOSE curDB
DEALLOCATE curDB;

-- Check staging table
SELECT * FROM admindba.dbo.ETLTempTable;
