-- ===========================================
-- Step 1: Collect ftpPassword + ClientCode (SourceCode/ClientID) from all databases into mytest
-- ===========================================
DECLARE @sql NVARCHAR(max)
DECLARE @DB sysname  

DECLARE curDB CURSOR FORWARD_ONLY STATIC FOR  
   SELECT [name]   
   FROM MSDB.sys.databases 
   WHERE [name] NOT IN ('master','model','msdb','tempdb','DefaultDB','admindba','iws_admin') 
     AND name NOT LIKE '%SP'
     AND name NOT LIKE '%snapshot%'
     AND name NOT LIKE '%stg'
     AND name NOT LIKE '%_old'
     AND state_desc NOT LIKE '%RESTORING%'
   ORDER BY [name];

-- Recreate staging table
IF OBJECT_ID('admindba.dbo.mytest','U') IS NOT NULL DROP TABLE admindba.dbo.mytest;
CREATE TABLE admindba.dbo.mytest
(
    dbname     VARCHAR(100),
    ClientCode NVARCHAR(200),        
    ftppass    NVARCHAR(256)
);

OPEN curDB  
FETCH NEXT FROM curDB INTO @DB  
WHILE @@FETCH_STATUS = 0  
BEGIN 
    IF OBJECT_ID(N'['+@DB+'].dbo.ETL_Configuration') IS NOT NULL  
    BEGIN 
        SET @sql = N'
        USE '+QUOTENAME(@DB)+';
        INSERT INTO admindba.dbo.mytest (dbname, ClientCode, ftppass)
        SELECT 
            DB_NAME() AS dbname,
            sc.ConfigurationValue AS ClientCode,       
            CONVERT(NVARCHAR(MAX),
                DECRYPTBYKEYAUTOCERT(
                    CERT_ID(''CertificateETLFTP''), 
                    NULL, 
                    pw.ConfigurationEncrypted
                )
            ) AS ftppass
        FROM dbo.ETL_Configuration sc
        INNER JOIN dbo.ETL_Configuration pw
            ON pw.ConfigurationName = ''ftpPassword''
        WHERE sc.ConfigurationName IN (''SourceCode'', ''ClientID'');';  -- multiple rows if multiple client codes
        
        PRINT @sql;  -- debug
        EXEC (@sql);
    END 
    FETCH NEXT FROM curDB INTO @DB  
END  

CLOSE curDB  
DEALLOCATE curDB;

-- Optional: review staging table
SELECT * FROM admindba.dbo.mytest;
