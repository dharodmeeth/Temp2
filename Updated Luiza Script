DECLARE @sql NVARCHAR(MAX),
        @DBLevel NVARCHAR(250),
        @DB sysname;

DECLARE curDB CURSOR FORWARD_ONLY STATIC FOR  
   SELECT [name]   
   FROM master..sysdatabases 
   WHERE [name] NOT IN ('master','model','msdb','tempdb','DefaultDB','admindba','iws_admin') 
     AND name NOT LIKE '%SP'
     AND name NOT LIKE '%snapshot%'
     AND name NOT LIKE '%stg'
   ORDER BY [name];

OPEN curDB  
FETCH NEXT FROM curDB INTO @DB  
WHILE @@FETCH_STATUS = 0  
BEGIN 
    IF OBJECT_ID(N'['+@DB+'].dbo.ETL_Configuration') IS NOT NULL  
    BEGIN 
        -- Get dbLevel from ETL_Configuration
        SET @sql = N'SELECT @DBLevel=ConfigurationValue 
                      FROM ['+@DB+'].dbo.ETL_Configuration 
                      WHERE ConfigurationName = ''dbLevel'';';
        EXEC sp_executesql @sql,
             N'@DBLevel NVARCHAR(250) OUTPUT',
             @DBLevel = @DBLevel OUTPUT;

        IF @DBLevel IS NOT NULL
        BEGIN 
            -- Insert or Update ClientInfo
            SET @sql = N'
            MERGE admindba.dbo.ClientInfo AS target
            USING (
                SELECT
                    (SELECT TOP 1 ConfigurationValue 
                     FROM ['+@DB+'].dbo.ETL_Configuration 
                     WHERE ConfigurationName = ''ClientID'') AS ClientCode,

                    (SELECT TOP 1 ConfigurationValue 
                     FROM ['+@DB+'].dbo.ETL_Configuration 
                     WHERE ConfigurationName = ''ftpUserName'') AS FtpUserName,

                    '''+@DBLevel+''' AS EtlVersion,

                    '''+@DB+''' AS DatabaseName,
                    CAST(SERVERPROPERTY(''MachineName'') AS NVARCHAR(200)) AS HostedServerName,
                    SYSUTCDATETIME() AS EventDate,
                    1 AS IsActive
            ) AS source
            ON (target.ClientCode = source.ClientCode)
            WHEN MATCHED THEN
                UPDATE SET 
                    target.FtpUserName      = source.FtpUserName,
                    target.EtlVersion       = source.EtlVersion,
                    target.DatabaseName     = source.DatabaseName,
                    target.HostedServerName = source.HostedServerName,
                    target.EventDate        = source.EventDate,
                    target.IsActive         = source.IsActive
            WHEN NOT MATCHED THEN
                INSERT (ClientCode, FtpUserName, EtlVersion, DatabaseName, HostedServerName, EventDate, IsActive)
                VALUES (source.ClientCode, source.FtpUserName, source.EtlVersion, source.DatabaseName, source.HostedServerName, source.EventDate, source.IsActive);';
            
            PRINT @sql;
            EXEC (@sql);
        END
    END 
    FETCH NEXT FROM curDB INTO @DB  
END  

CLOSE curDB  
DEALLOCATE curDB;

-- Review results
SELECT * FROM admindba.dbo.ClientInfo;
