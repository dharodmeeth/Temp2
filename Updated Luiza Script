DECLARE @sql NVARCHAR(MAX),
        @DBLevel NVARCHAR(250),
        @DB sysname;

DECLARE curDB CURSOR FORWARD_ONLY STATIC FOR  
   SELECT [name]   
   FROM master..sysdatabases 
   WHERE [name] NOT IN ('master','model','msdb','tempdb','DefaultDB','admindba','iws_admin') 
     AND name NOT LIKE '%SP'
     AND name NOT LIKE '%snapshot%'
     AND name NOT LIKE '%stg'
   ORDER BY [name];

OPEN curDB  
FETCH NEXT FROM curDB INTO @DB  
WHILE @@FETCH_STATUS = 0  
BEGIN 
    IF OBJECT_ID(N'['+@DB+'].dbo.ETL_Configuration') IS NOT NULL  
    BEGIN 
        -- Get dbLevel from ETL_Configuration
        SET @sql = N'SELECT @DBLevel=ConfigurationValue 
                      FROM ['+@DB+'].dbo.ETL_Configuration 
                      WHERE ConfigurationName = ''dbLevel'';';
        EXEC sp_executesql @sql,
             N'@DBLevel NVARCHAR(250) OUTPUT',
             @DBLevel = @DBLevel OUTPUT;

        IF @DBLevel IS NOT NULL
        BEGIN 
            -- Insert into dbo.ClientInfo
            SET @sql = N'
            INSERT INTO admindba.dbo.ClientInfo (ClientCode, DxraFtpAccount, EtlVersion, DatabaseName, HostedServerName, EventDate, IsActive)
            SELECT 
                (SELECT TOP 1 ConfigurationValue 
                 FROM ['+@DB+'].dbo.ETL_Configuration 
                 WHERE ConfigurationName = ''ClientID'') AS ClientCode,

                (SELECT TOP 1 ConfigurationValue 
                 FROM ['+@DB+'].dbo.ETL_Configuration 
                 WHERE ConfigurationName = ''ftpUserName'') AS DxraFtpAccount,

                '''+@DBLevel+''' AS EtlVersion,

                '''+@DB+''' AS DatabaseName,
                CAST(SERVERPROPERTY(''MachineName'') AS NVARCHAR(200)) AS HostedServerName,
                SYSUTCDATETIME() AS EventDate,
                1 AS IsActive;';
            
            PRINT @sql;
            EXEC (@sql);
        END
    END 
    FETCH NEXT FROM curDB INTO @DB  
END  

CLOSE curDB  
DEALLOCATE curDB;

SELECT * FROM admindba.dbo.ClientInfo;
